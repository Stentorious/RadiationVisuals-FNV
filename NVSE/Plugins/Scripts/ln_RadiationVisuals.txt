; Radiation Visuals - Stentorious

; On game load
RIMOD HouseDecorationRaider
Goo1.AuxVarSetFlt "*RadVis_IMOD" 0
SetUIFloatGradual "HUDMainMenu\RadiationVisuals\_RadEnvironment" 0
SetUIFloatGradual "HUDMainMenu\RadiationVisuals\_RadInstant" 0

; On game restart
if eval Goo1.AuxVarGetFlt "*RadVis_Init"
	return
endif
Goo1.AuxVarSetFlt "*RadVis_Init" 1

; Check for requirements
if eval GetNVSEVersion < 6 || (GetNVSEVersion == 6 && GetNVSERevision < 4)
	MessageBoxEx "Radiation Visuals missing requirement!%rInstall xNVSE 6.4.0+."
	return
endif
if GetPluginVersion "JohnnyGuitarNVSE" < 455
	MessageBoxEx "Radiation Visuals missing requirement!%rInstall latest JohnnyGuitar NVSE plugin."
	return
endif
if GetPluginVersion "ShowOffNVSE Plugin" < 165
	MessageBoxEx "Radiation Visuals missing requirement!%rInstall latest ShowOff NVSE plugin."
	return
endif
if GetPluginVersion "UI Organizer Plugin" < 230
	MessageBoxEx "Radiation Visuals missing requirement!%rInstall latest User Interface Organizer plugin."
	return
endif

; Load INI config
SetUIFloatAlt "HUDMainMenu\RadiationVisuals\_ParticleStrength" (Clamp (GetINIFloat_Cached "Effect:fParticleStrength" "Stentorious\RadiationVisuals.ini" 1.2) 0 2)
SetUIFloatAlt "HUDMainMenu\RadiationVisuals\_StaticStrength" (Clamp (GetINIFloat_Cached "Effect:fStaticStrength" "Stentorious\RadiationVisuals.ini" 0.15) 0 1)
SetUIFloatAlt "HUDMainMenu\RadiationVisuals\_RadMin" (GetMaxOf 0 (GetINIFloat_Cached "General:fRadiationMin" "Stentorious\RadiationVisuals.ini" 0))
SetUIFloatAlt "HUDMainMenu\RadiationVisuals\_RadMax" (GetMaxOf (GetUIFloatAlt "HUDMainMenu\RadiationVisuals\_RadMin") (GetINIFloat_Cached "General:fRadiationMax" "Stentorious\RadiationVisuals.ini" 15))

Goo1.AuxVarSetFlt "*_RadVis_INI" (Clamp (GetINIFloat_Cached "Effect:fBlurStrength" "Stentorious\RadiationVisuals.ini" 0.5) 0 1) 0
Goo1.AuxVarSetFlt "*_RadVis_INI" (Clamp (GetINIFloat_Cached "Effect:fDoubleVisionStrength" "Stentorious\RadiationVisuals.ini" 0.2) 0 1) 1
Goo1.AuxVarSetFlt "*_RadVis_INI" (Clamp (GetINIFloat_Cached "Effect:fSaturationStrength" "Stentorious\RadiationVisuals.ini" 0.3) -1 5) 2
Goo1.AuxVarSetFlt "*_RadVis_INI" (Clamp (GetINIFloat_Cached "Effect:fContrastStrength" "Stentorious\RadiationVisuals.ini" 0) -1 1) 3
Goo1.AuxVarSetFlt "*_RadVis_INI" (Clamp (GetINIFloat_Cached "Effect:fBrightnessStrength" "Stentorious\RadiationVisuals.ini" 0) -1 1) 4
Goo1.AuxVarSetFlt "*_RadVis_INI" (eval GetINIFloat_Cached "General:bInstantaneousVisuals" "Stentorious\RadiationVisuals.ini" 1) 5

SetUIFloatAlt "HUDMainMenu\RadiationVisuals\_ParticleSystemcolor" (Clamp (GetINIFloat_Cached "Color:iParticleSystemcolor" "Stentorious\RadiationVisuals.ini" 0) 0 5)
SetUIFloatAlt "HUDMainMenu\RadiationVisuals\_ParticleRed" (Clamp (GetINIFloat_Cached "Color:iParticleRed" "Stentorious\RadiationVisuals.ini" 0) 0 255)
SetUIFloatAlt "HUDMainMenu\RadiationVisuals\_ParticleGreen" (Clamp (GetINIFloat_Cached "Color:iParticleGreen" "Stentorious\RadiationVisuals.ini" 255) 0 255)
SetUIFloatAlt "HUDMainMenu\RadiationVisuals\_ParticleBlue" (Clamp (GetINIFloat_Cached "Color:iParticleBlue" "Stentorious\RadiationVisuals.ini" 33) 0 255)

; Helmet overlay support
if eval IsModLoaded "Helmet Overlay.esm"
	Goo1.AuxVarSetFlt "*_Helmet Overlay.esm" 1
	SetUIFloatAlt "HUDMainMenu\RadiationVisuals\_HelmetOverlayDepth" (GetINIFloat_Cached "General:bPrioritizeHelmetOverlay" "Stentorious\RadiationVisuals.ini" 1)
endif

; Disable MUX's radiation visuals
SetUIFloatAlt "HUDMainMenu\RadiationMeter\MUX_Radiation\Effect\visible" 0

; Animation control
SetUIFloatGradual "HUDMainMenu\RadiationVisuals\ParticleWeak\_index" 0 10.999 0.55 3
SetUIFloatGradual "HUDMainMenu\RadiationVisuals\ParticleAverage\_index" 0 9.999 0.5 3
SetUIFloatGradual "HUDMainMenu\RadiationVisuals\ParticleStrong\_index" 0 8.999 0.45 3

; Set IMOD traits
SetImageSpaceModTrait HouseDecorationRaider 37 1	; Saturation (Mult)
SetImageSpaceModTrait HouseDecorationRaider 38 0	; Saturation (Add)
SetImageSpaceModTrait HouseDecorationRaider 39 1	; Contrast (Mult)
SetImageSpaceModTrait HouseDecorationRaider 40 0	; Contrast (Add)
SetImageSpaceModTrait HouseDecorationRaider 42 0	; Contrast (Add)
SetImageSpaceModTrait HouseDecorationRaider 47 255	; Tint (Red)
SetImageSpaceModTrait HouseDecorationRaider 48 255	; Tint (Green)
SetImageSpaceModTrait HouseDecorationRaider 49 255	; Tint (Blue)
SetImageSpaceModTrait HouseDecorationRaider 50 0	; Tint (Alpha)

; Main Loop
SetGameMainLoopCallback (CompileScript "RadiationVisuals\MainLoop.gek") 1 10 3

; Reset effect on fast travel
SetOnFastTravelEventHandler (begin Function {ref rMapMarker}
	RIMOD HouseDecorationRaider
	Goo1.AuxVarSetFlt "*RadVis_IMOD" 0
	SetUIFloatGradual "HUDMainMenu\RadiationVisuals\_RadEnvironment" 0
	SetUIFloatGradual "HUDMainMenu\RadiationVisuals\_RadInstant" 0
end) 1

; Update effect on instantaneous radiation (Eg. Eating food)
SetOnActorValueChangeEventHandler 1 (begin Function {int iAVCode, float fPreviousValue, float fNewValue}
	if eval Goo1.AuxVarGetFlt "*_RadVis_INI" 5 && (fNewValue -= fPreviousValue) > 0 && Player.GetRadiationLevelAlt == 0
		SetUIFloatGradual "HUDMainMenu\RadiationVisuals\_RadInstant" fNewValue 0 2
	endif
end) 0 54

CloseFileSO "Stentorious\RadiationVisuals.ini" 0
