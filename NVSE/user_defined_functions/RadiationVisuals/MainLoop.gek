float fRadiationTarget
float fRadiationCurrent
float fEffectControl

begin Function {}

	fRadiationTarget = Player.GetRadiationLevelAlt
	fRadiationCurrent = GetUIFloatAlt "HUDMainMenu\RadiationVisuals\_RadEnvironment"

	if eval fRadiationCurrent != fRadiationTarget
		SetUIFloatGradual "HUDMainMenu\RadiationVisuals\_RadEnvironment" fRadiationCurrent fRadiationTarget 0.2
	endif

	; Update effect
	if eval (fEffectControl = GetUIFloatAlt "HUDMainMenu\RadiationVisuals\_EffectControl") > 0

		SetImageSpaceModTrait HouseDecorationRaider 45 (fEffectControl * (Goo1.AuxVarGetFlt "*RadVis_INI" 0) * 2.5)
		SetImageSpaceModTrait HouseDecorationRaider 46 (fEffectControl * (Goo1.AuxVarGetFlt "*RadVis_INI" 1) * 0.025)
		SetImageSpaceModTrait HouseDecorationRaider 37 (1 + (fEffectControl * (Goo1.AuxVarGetFlt "*RadVis_INI" 2)))
		SetImageSpaceModTrait HouseDecorationRaider 41 (1 + (fEffectControl * (Goo1.AuxVarGetFlt "*RadVis_INI" 3) * 0.2))
		SetImageSpaceModTrait HouseDecorationRaider 43 (1 + (fEffectControl * (Goo1.AuxVarGetFlt "*RadVis_INI" 4) * 3))
		SetImageSpaceModTrait HouseDecorationRaider 50 (fEffectControl * (Goo1.AuxVarGetFlt "*RadVis_INI" 6) * 255)

		; Apply IMOD
		if Goo1.AuxVarGetFlt "*RadVis_IMOD" == 0
			IMOD HouseDecorationRaider
			Goo1.AuxVarSetFlt "*RadVis_IMOD" 1
		endif

		; Randomize texture rotation
		SetUIFloatAlt "HUDMainMenu\RadiationVisuals\_Random" (GetRandomInRange 0 16)

	; Remove IMOD
	elseif eval Goo1.AuxVarGetFlt "*RadVis_IMOD"
		RIMOD HouseDecorationRaider
		Goo1.AuxVarSetFlt "*RadVis_IMOD" 0
	endif

end